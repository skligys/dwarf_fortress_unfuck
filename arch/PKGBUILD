# Maintainer: Skirmantas Kligys <Skirmantas.Kligys@gmail.com>
# Modified from: https://www.archlinux.org/packages/community/x86_64/dwarffortress/
pkgname=dwarffortress-sk
pkgver=0.47.03
_pkgver=47_03
_pkgname=dwarffortress
pkgrel=1
pkgdesc="A single-player fantasy game in which you build a dwarven outpost or play an adventurer in a randomly generated world"
arch=(x86_64)
url="http://www.bay12games.com/dwarves/"
license=('custom:dwarffortress')
depends=(gtk3 glu sdl2 sdl2_image libsndfile openal sdl2_ttf glew gcc-libs glib2)
makedepends=(git cmake patchelf)
optdepends=('nvidia-utils: If you have nvidia graphics'
            'alsa-lib: for alsa sound'
            'libpulse: for pulse sound')
options=('!strip' '!buildflags')
install=${_pkgname}.install
# I made a fucking github repo with the sole purpose of unfucking df a bit
# We try to compile whatever little bit of df is open source
# To use local git repository: git+file:///home/skirmis/Work/DwarfFortressSdl2/dwarf_fortress_unfuck
source=(git://github.com/skligys/dwarf_fortress_unfuck.git#tag=${pkgver}
        dwarffortress
        dwarffortress.desktop
        dwarffortress.png
        http://www.bay12games.com/dwarves/df_${_pkgver}_linux.tar.bz2)
sha256sums=('SKIP'
            'eeb579e38a999cb171366137fe38b5cff8cd60f60d526dc25eed3e8e5058159d'
            '17cbc5699d7b1f6c2c4ec5d8b53eb945c9936845c516f81b7a1db22fd3276b3c'
            '83183abc70b11944720b0d86f4efd07468f786b03fa52fe429ca8e371f708e0f'
            '1a33a1be06f6cdd08f94e39c4433ab35dc8ad0a7e319e8d19436f7d473ed785e')
conflicts=(dwarffortress dwarffortress-obsidian dwarffortress-ironhand dwarffortress-phoebus dwarffortress-spacefox)
provides=("dwarffortress=$pkgver")

build() {
  cd dwarf_fortress_unfuck

  cmake .
  make
}

package() {
  install -dm755 "$pkgdir"/opt/
  cp -r "$srcdir"/df_linux "$pkgdir"/opt/$_pkgname
  rm -r "$pkgdir"/opt/$_pkgname/df "$pkgdir"/opt/$_pkgname/libs/* "$pkgdir"/opt/$_pkgname/g_src

  find "$pkgdir"/opt/$_pkgname -type d -exec chmod 755 {} +
  find "$pkgdir"/opt/$_pkgname -type f -exec chmod 644 {} +

  patchelf --remove-needed libSDL-1.2.so.0 "$srcdir"/df_linux/libs/Dwarf_Fortress
  install -Dm755 "$srcdir"/df_linux/libs/Dwarf_Fortress "$pkgdir"/opt/$_pkgname/libs/Dwarf_Fortress
  install -Dm755 "$srcdir"/dwarf_fortress_unfuck/libgraphics.so "$pkgdir"/opt/$_pkgname/libs/libgraphics.so
  install -Dm755 "$srcdir"/dwarffortress "$pkgdir"/usr/bin/$_pkgname

  # No idea why we need this. Really. This isn't being loaded dynamically, it's not linked and
  # in general there is no indication this is being used. However, it doesn't work without this symlink.
  ln -s /usr/lib/libpng.so "$pkgdir"/opt/$_pkgname/libs/libpng.so.3

  # Set pkgname in runscript
  sed -i "s/^pkgname=.*/pkgname=$_pkgname/" "$pkgdir"/usr/bin/$_pkgname

  # Desktop launcher with icon
  install -Dm644 "$srcdir"/dwarffortress.desktop "$pkgdir"/usr/share/applications/"$_pkgname".desktop
  install -Dm644 "$srcdir"/dwarffortress.png     "$pkgdir"/usr/share/pixmaps/"$_pkgname".png

  install -Dm644 "$srcdir"/df_linux/readme.txt "$pkgdir"/usr/share/licenses/$_pkgname/readme.txt
}
